plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'fabric-loom' version '0.2.6-SNAPSHOT'
}

repositories {
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://libraries.minecraft.net/' }
}

def minecraftVersion = "1.14.4"
def yarnBuild = "build.15"
def loaderVersion = "0.7.2+build.174"

dependencies {
    // Fabric Stuff, We don't specifically target only a single version but yarn mappings require a version to be specified.
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${minecraftVersion}+${yarnBuild}:v2"
    modCompile "net.fabricmc:fabric-loader:${loaderVersion}"

    // We only include the bare minimum of what we use within the Fabric API.

    modApi "net.fabricmc.fabric-api:fabric-api-base:0.1.2+b7f9825de8"
    modApi "net.fabricmc.fabric-api:fabric-commands-v0:0.1.2+b7f9825de8"
    modApi "net.fabricmc.fabric-api:fabric-events-lifecycle-v0:0.1.2+b7f9825de8"
    modApi "net.fabricmc.fabric-api:fabric-networking-v0:0.1.7+12515ed9e8"

    compile project(':common')
}

task remapJar(type: net.fabricmc.loom.task.RemapJarTask, overwrite: true) {
    dependsOn shadowJar
}


processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task jar(type: Jar, overwrite: true) {
    classifier = "compile"
    from compileJava
    from processResources
}

shadowJar {
    archiveName = "LuckPerms-Fabric-${project.ext.fullVersion}.jar"

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
    }

    relocate 'net.kyori.text', 'me.lucko.luckperms.lib.text'
    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'me.lucko.commodore', 'me.lucko.luckperms.lib.commodore'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
}

artifacts {
    archives shadowJar
}